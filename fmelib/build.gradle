apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.tananaev'
version = '1.2'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 25
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    task androidJavadocs(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
    }

    task androidJavadocsJar(type: Jar) {
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    artifacts {
        archives androidJavadocsJar, androidSourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'fmelib'
                    packaging 'jar'
                    description 'Fragments Made Easy library for Android.'
                    url 'https://github.com/tananaev/fmelib'

                    scm {
                        connection 'scm:git:https://github.com/tananaev/fmelib'
                        developerConnection 'scm:git:git@github.com:tananaev/fmelib.git'
                        url 'https://github.com/tananaev/fmelib'
                    }

                    licenses {
                        license {
                            name 'Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id 'tananaev'
                            name 'Anton Tananaev'
                            email 'anton.tananaev@gmail.com'
                        }
                    }
                }
            }
        }
    }

}
